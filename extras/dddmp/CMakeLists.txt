set(DDDMP_PUBLIC_HEADERS
  "${CMAKE_CURRENT_SOURCE_DIR}/include/cudd/dddmp.h"
)

set(DDDMP_SOURCES
  "${CMAKE_CURRENT_SOURCE_DIR}/src/dddmpInt.h"
  "${CMAKE_CURRENT_SOURCE_DIR}/src/dddmpBinary.c"
  "${CMAKE_CURRENT_SOURCE_DIR}/src/dddmpNodeAdd.c"
  "${CMAKE_CURRENT_SOURCE_DIR}/src/dddmpStoreCnf.c"
  "${CMAKE_CURRENT_SOURCE_DIR}/src/dddmpConvert.c"
  "${CMAKE_CURRENT_SOURCE_DIR}/src/dddmpNodeBdd.c"
  "${CMAKE_CURRENT_SOURCE_DIR}/src/dddmpStoreMisc.c"
  "${CMAKE_CURRENT_SOURCE_DIR}/src/dddmpDbg.c"
  "${CMAKE_CURRENT_SOURCE_DIR}/src/dddmpNodeCnf.c"
  "${CMAKE_CURRENT_SOURCE_DIR}/src/dddmpUtil.c"
  "${CMAKE_CURRENT_SOURCE_DIR}/src/dddmpLoad.c"
  "${CMAKE_CURRENT_SOURCE_DIR}/src/dddmpStoreAdd.c"
  "${CMAKE_CURRENT_SOURCE_DIR}/src/dddmpLoadCnf.c"
  "${CMAKE_CURRENT_SOURCE_DIR}/src/dddmpStoreBdd.c"
)

if(CUDD_BUILD_SHARED_LIBS)
  add_library(dddmp SHARED ${DDDMP_SOURCES})
else()
  add_library(dddmp STATIC ${DDDMP_SOURCES})
endif()

target_include_directories(dddmp PUBLIC
  $<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}/include>
  $<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}/src>
  $<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}/extras/dddmp/include>
  $<BUILD_INTERFACE:${CMAKE_SOURCE_DIR}/extras/dddmp/src>
  $<INSTALL_INTERFACE:include>
)

set_target_properties(dddmp PROPERTIES
  VERSION ${PROJECT_VERSION}
  SOVERSION ${PROJECT_VERSION_MAJOR}
  POSITION_INDEPENDENT_CODE ON
  CXX_VISIBILITY_PRESET hidden
  VISIBILITY_INLINES_HIDDEN ON
  PUBLIC_HEADER "${DDDMP_PUBLIC_HEADERS}"
)

install(TARGETS dddmp
  EXPORT cuddTargets
  RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR} COMPONENT Runtime
  LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR} COMPONENT Runtime 
  ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR} COMPONENT Development
  INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
  PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/cudd COMPONENT Development
)